plugins {
    id 'java'
}

group 'michael_mario_kubicki'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // Lucene
    compile group: 'org.apache.lucene', name: 'lucene-core', version: '7.6.0'
    compile group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: '7.6.0'
    compile group: 'org.apache.lucene', name: 'lucene-codecs', version: '7.6.0'
    compile group: 'org.apache.lucene', name: 'lucene-queryparser', version: '7.6.0'
    //Jsoup
    compile 'org.jsoup:jsoup:1.11.3'
}

jar {
    // FROM https://github.com/lukhnos/lucenestudy/blob/master/build.gradle
    // From http://cmoz.me/blog/2014/11/service-files-uber-jars-and-gradle/
    doFirst {
        def serviceDir = file("$buildDir/META-INF/services")
        serviceDir.deleteDir()
        serviceDir.mkdirs()

        // copy all service files from deps to buildDir
        for(file in configurations.runtime) {
            zipTree(file).matching{ include 'META-INF/services/*' }.each { f ->
                new File(serviceDir, f.name) << f.getText("UTF-8")
            }
        }
    }

    manifest {
        attributes "Main-Class": "Main"
    }

    from(configurations.runtime.collect{ it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/**' // Don't let Gradle merge service files
    }

    // Include service files from the buildDir.
    from fileTree(buildDir).matching{ include 'META-INF/services/*' }
}
